plugins {
    id 'java'
    id 'com.gradleup.shadow' version '9.2.0'
}

group = 'pl.norbit'
version = 'v1.9.2'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
    maven {
        url = 'https://repo.extendedclip.com/releases/'
    }
    maven { url "https://maven.enginehub.org/repo/" }
    maven { url 'https://jitpack.io' }
    maven {
        url = 'https://repo.extendedclip.com/content/repositories/placeholderapi/'
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.21.8-R0.1-SNAPSHOT"
    //bstats
    implementation 'org.bstats:bstats-bukkit:3.1.0'
    //glowing
    implementation 'fr.skytasul:glowingentities:1.4.8'
    //worldguard
    compileOnly 'com.sk89q.worldguard:worldguard-bukkit:7.0.8'
    //itemsadder
    compileOnly 'com.github.LoneDev6:API-ItemsAdder:3.6.2-beta-r3-b'

    compileOnly 'me.clip:placeholderapi:2.11.6'
    //lombok
    compileOnly 'org.projectlombok:lombok:1.18.32'
    annotationProcessor 'org.projectlombok:lombok:1.18.32'
    //configapi
    implementation 'net.serlith.ConfigAPI:ConfigAPI-core:1.2.5'
}

compileJava {options.encoding = "UTF-8"}

def targetJavaVersion = 21

shadowJar {
    relocate 'org.bstats', 'pl.norbit.treecuter.lib.bstats'
    relocate 'fr.skytasul', 'pl.norbit.treecuter.lib.skytasul'
    relocate 'net.j4c0b3y.api.config', 'pl.norbit.treecuter.lib.config'
    relocate 'dev.dejvokep.boostedyaml', 'pl.norbit.treecuter.lib.boostedyaml'
    archiveClassifier.set("")
}

tasks.build {
    dependsOn("shadowJar")
}

java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 17 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
    filesMatching('paper-plugin.yml') {
        expand props
    }
}
